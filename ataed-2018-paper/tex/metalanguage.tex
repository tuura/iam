
In the previous section we have described the semantics of several instructions of
a generic computer architecture in terms of a polymorphic computational metalanguage.
This section presents the formal definition of the metalanguage and provides a more
formal description of the instruction and program semantics.

\textbf{A remark on formal definitions}
Before we start, let us make a remark on what we consider a formal definition.
We do not aim to formalise our tools in any kind of foundational mathematical system,
such as ZF\footnote{Zermelo-Fraenkel set theory.} or homotopy type theory. We are
presenting an elegant way of solving a well-known problem and we use the Haskell
programming language to implement the solution. Therefore, we consider a concept
to be~\emph{formally} defined if it is expressed as a Haskell data type. This may
sound hand-wavy, but since Haskell has a static type system (a variant of
System~F~\cite{Sulzmann:2007:SFT:1190315.1190324}) and operational semantics, we
can be formal enough.

\textbf{Definition (polymorphic computational metalanguage):\label{def:metalanguage}}
A term of the metalanguage is a value of the following Haskell type:

\begin{minted}[xleftmargin=10pt]{haskell}
type Semantics c a =
    forall f. c f => (Key -> f Value)
                  -> (Key -> f Value -> f ())
                  -> Maybe (f a)
\end{minted}

\noindent A~\hs{Semantics} is essentially a rank-2 polymorphic\footnote{A rank-2 polymorphic
function is one taking as a parameter another function, which is in turn (rank-1)
polymorphic. This feature requires the~\hs{RankNTypes} language extension of the
Glasgow Haskell Compiler.}
effectful computation depending on two functions,
which we will usually refer to as~\hs{read} and~\hs{write}.

Let us now give some intuition for the components of the metalanguage.
The~\hs{Semantics c a}
type may be thought as a mutable dictionary. The~\hs{read} function has
type~\hs{Key -> f Value} --- it takes
a key and gives back an effectful value looked up in the dictionary. The~\hs{write}
function takes a key and an effectful value and alters the value of the key in
the dictionary. The semantics can be partial, hence the the return type~\hs{f a}
is wrapped in the \hs{Maybe} type constructor.
\hs{Maybe}\footnote{Defined in the Haskell's \textsf{base} library as~\hs{data Maybe a = Just a | Nothing}.}
is an idiomatic Haskell encoding of partial definitions. The semantics may
become partial if we, for example, fix the constraint type variable~\hs{c}
to~\hs{Applicative}, thus losing the possibility to encode the monadic
components of the instruction set. \hs{Maybe} allows us to treat such
partially-defined semantics in a safe and formal way.

\textbf{Definition (Instruction Set):} An~\emph{instruction set} is an algebraic
data type with as many data constructors as there are instructions.
If an instruction has an argument, it is defined as an argument of the
corresponding data constructor.

Consider an example definition of an instruction set consisting
of instructions described in the previous sections and the related auxiliary types:

\begin{minted}[xleftmargin=10pt]{haskell}
data Instruction = Load   Register MemoryAddress
                 | LoadMI Register MemoryAddress
                 | Add    Register MemoryAddress
                 | Jump   Value

data Register = R0 | R1 | R2 | R3

type MemoryAddress = Value
\end{minted}

\noindent
\textbf{Definition (Instruction Set Semantics):}
The \emph{semantics of an instruction set} is a Haskell function mapping
data constructors of the instruction set to the terms of the polymorphic
computational metalanguage.

The definition of an instruction set semantics is the point where the metalanguage
has to be made monomorphic, i.e. the context constraint has to be instantiated with
a concrete one. Below we present unrestricted, functorial, applicative and
monadic semantics for the defined instruction set.

We start from the~\hs{Load} instruction which may be executed in any
context\footnote{The~\hs{Unrestricted} constraint is not exactly idiomatic Haskell and
requires some tricks to be defined.}:

\vspace{-1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
semanticsU :: Instruction -> Semantics Unrestricted ()
semanticsU (Load reg addr) = load reg addr
semanticsU _               = const (const Nothing)
\end{minted}

Note that the Haskell wildcard pattern `\hs{_}' is used to match all instructions
that require a more restrictive context. The~\hs{const (}\hs{const Nothing)} expression
is equivalent to~\hs{\read write -> Nothing} and constructs a stub for these
more restricted semantics. The function \hs{load} has been defined
in~\S\ref{sec:instructions}.

The instantiation of~\hs{c} with a~\hs{Functor} allows us to implement the
semantics of the instruction~\hs{Jump}:

\begin{minted}[xleftmargin=10pt]{haskell}
semanticsF :: Instruction -> Semantics Functor ()
semanticsF (Jump simm) = jump simm
semanticsF i           = semanticU i
\end{minted}

\noindent Here we use the definition of \hs{jump} from~\S\ref{sec:instructions}
and fall back to unrestricted semantics definition \hs{semanticU} for
the~\hs{Load} instruction, hence avoiding code duplication.

The remaining definitions are analogous:

\begin{minted}[xleftmargin=10pt]{haskell}
semanticsA :: Instruction -> Semantics Applicative ()
semanticsA (Add reg addr) = add reg addr
semanticsA i              = semanticsF i

semanticsM :: Instruction -> Semantics Monad ()
semanticsM (LoadMI reg addr) = loadMI reg addr
semanticsM i                 = semanticsA i
\end{minted}

We can now define the semantics of a block of instructions by reducing
a given list of instructions:

\begin{minted}[xleftmargin=10pt]{haskell}
blockSemanticsA :: [Instruction] -> Semantics Applicative ()
blockSemanticsA xs = \r w ->
    foldr (\x acc -> (*>) <$> acc <*> semanticsA x r w) nop xs
    where nop = Just $ pure ()
\end{minted}

\noindent The semantics of an empty block is~\hs{nop} (i.e. a \emph{no-op}
instruction). The semantics of a non-empty list is the semantics of its head
chained with the semantics of the tail. We need to lift the applicative chaining
operation since the~\hs{Maybe} type constructor also is an instance
of~\hs{Applicative} and the behaviour of~\hs{*>} returns the contents of the
last~\hs{Just}, which is would be wrong.

Now, with the instruction semantics defined in terms of the polymorphic
computational metalanguage, we may proceed to evaluating the metalanguage
in concrete contexts to get a practical interpretation of the instruction set.

The next section presents the interpretation of unrestricted, functorial and
applicative instructions yielding concurrency oracles for programs.

Other interpretations of the metalanguage are also possible. In the technical
report~\cite{mokhov2018formal} we present a formal model of a processor
developed for space missions, where we use a more restricted, monadic
metalanguage, making emphasis on symbolic program execution and automated
theorem proving: the framework allows to verify functional properties of
programs and automatically check if two programs are semantically equivalent.
The metalanguage presented in this paper also allows these interpretations,
but the focus of this paper is different: automated derivation of concurrency
oracles.